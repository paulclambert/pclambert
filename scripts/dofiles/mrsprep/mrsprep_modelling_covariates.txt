---
title: "Modelling covariates in marginal relative survival models"
---

```stata
<<dd_do: quietly>>
clear frames
set scheme fpsaus_c
<</dd_do>>
```

## Background

I have described how to fit a [marginal relative survival moded](mrsprep_marginal_relative_survival.qmd) to give an 
internally (age) standardized estimate and how this can be extended to give an [externally age standardized](mrsprep_external_age_standardization.qmd) estimate.
This example shows how to incorporate covariates into the marginal model whilst, still age standardizing. 


## Example

I again use the Melanoma data, restricting to those diagnosed in the later calendar perdiod, 1985-1994, but will compare relative survival between males and females.
I restrict follow-up to 10 years after diagnosis using the `exit()` option. 

```stata
<<dd_do>>
use https://pclambert.net/data/melanoma.dta if year8594 == 1 
stset surv_mm, failure(status=1,2) id(id) exit(time 120.5) scale(12)
gen female = sex==2
<</dd_do>>
```

I will first estimate the non-parametric estimate of marginal relative survival using [`stpp`](/software/stpp). 
I will use the `by(female)` option to estimate separately for males and females. As the age distribution between males and females
could potentially be different I need to age standardize. I will use the ICSS weights (Corazziari *et al.*). 

```stata
<<dd_do >>
// change age groups to those defined in ICSS
drop agegrp
egen agegrp=cut(age), at(0  45 55 65 75 200) icodes
replace agegrp = agegrp + 1
label variable agegrp "Age group"
label define agegrplab 1 "0-44" 2 "45-54" 3 "55-64" 4 "65-74" 5 "75+", replace
label values agegrp agegrplab

recode agegrp (1=0.28) (2=0.17) (3=0.21) (4=0.20) (5=0.14), gen(ICSSwt)
<</dd_do>>
```

The relative weights (explained in the [example on external age standardization](mrsprep_external_age_standardization.qmd)) have to been
calculated separately for males and females. This can be done as follows.

```stata
<<dd_do >>
//Proportion within each age group by sex to calculate weights
bysort female: egen totalsex = total(sex)
bysort agegrp female: gen a_age_sex = _N/totalsex
gen double wt_age_sex = ICSSwt/a_age_sex
<</dd_do>>
```

The non-parametric Pohar estimator can be obtained using [`stpp`](/software/stpp.qmd). 

```stata
<<dd_do >>
stpp R_pp using https://pclambert.net/data/popmort.dta, /// 
                agediag(age) datediag(dx) pmother(sex)  ///
                by(female)                              ///
                indweights(wt_age_sex)
frame put R_pp* female _t, into(PP)
<</dd_do>>
```

I have saved the Pohar Perme estimates in a frame, so I can plot them after using `mrsprep`.


## Using `mrsprep` to enable modelling of covariates

As the individual level weights have been calculated all that has to be added to `mrsprep` is the `by(female)` option.
This will calculate the mean expected mortality rate needed to fit the model separately for males and females. The individual weights
are incorporated into both the weighted mean expected mortality rate and the time-dependent weights.

```stata
<<dd_do >>
mrsprep using https://pclambert.net/data/popmort.dta   ///
              , pmother(sex) agediag(age) datediag(dx) ///
                breaks(0(0.2)10)                       ///
                indweights(wt_age_sex)                 ///
                by(female)
<</dd_do>>
```


Modelling proceeds as before, but now we can model the effect of sex.
A proportional excess hazards marginal model can be fitted as follows,


```stata
<<dd_do >>
stset tstop [iweight=wt], enter(tstart) failure(event==1)					   
stpm3 female, scale(lncumhazard) df(5) bhazard(meanhazard_wt) vce(cluster id) eform
<</dd_do>>
```

This gives a marginal excess hazard (mortality rate) ratio of  <<dd_display: %4.2f `=exp(_b[female])'>>.
Note we would expect this be different from a standard (conditional) relative survival model adjusting for age due
to the non collapsability of (excess) hazard ratios.

The proportionality assumption can be relaxed by incorporating an interaction
between sex and the effect of time from diagnosis.

```stata
<<dd_do >>
stpm3 female, scale(lncumhazard) df(5) bhazard(meanhazard_wt) vce(cluster id) ///
       tvc(female) dftvc(3)
predict s_mrs_male s_mrs_female, surv timevar(0 10, step(0.1)) ci frame(mrs) ///
                                 at1(female 0) at2(female 1)
<</dd_do>>
```
I have predicted marginal relative survival separately for males and females.
These can be shown in the plot below.


```stata
<<dd_do >>
frame PP {
  twoway (rarea R_pp_lci R_pp_uci _t if !female, sort connect(stairstep) color(%30))      ///
         (rarea R_pp_lci R_pp_uci _t if female,  sort connect(stairstep) color(%30))      ///
         (line R_pp _t if !female, sort lpattern(dot) connect(stairstep) pstyle(p1line))  ///
         (line R_pp _t if female,  sort lpattern(dot) connect(stairstep) pstyle(p2line)), ///
         ylabel(0.6(0.1)1, format(%3.1f))                                                      ///
         ytitle("Marginal relative survival")                                                  ///
         xtitle("Years from diagnosis")                                                        ///
         name(sex_compare, replace)      
}         
frame mrs: addplot: (line s_mrs_male* tt,   sort pstyle(p1line..) lpattern(solid dash dash)) 
frame mrs: addplot: (line s_mrs_female* tt, sort pstyle(p2line..) lpattern(solid dash dash) ///
                     legend(order(5 "Males" 8 "Females")                                     ///
                            ring(0) cols(1) pos(7)))                 
<</dd_do>>
```
<<dd_do: quietly>>
graph export ${DRIVE}/github/pclambert/statasvg/mrsprep_sex_compare.svg, replace width(700px)
<</dd_do>>

![](/statasvg/mrsprep_sex_compare.svg)

Thus we have obtained externally age standardized estimates of marginal relative survival without the
need to stratify or model the effect of age.

## References

Lambert PC, Syriopoulou E,  Rutherford MJ. [Direct modelling of age standardized marginal relative survival through incorporation of time-dependent weights.](https://doi.org/10.1186/s12874-021-01266-1)
*BMC Medical Research Methodology* 2021;**21**:84 

Corazziari I, Quinn M, Capocaccia R. Standard cancer patient population for age standardising survival ratios. 
*European Journalo of Cancer* 2004;**40**:2307-2316 
