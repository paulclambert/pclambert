---
title: "RMST of Standardized Survival Functions"
---


<<dd_do: quietly>>
set scheme fpsaus_c 
<</dd_do>>


Here I will show another useful measure from standardized survival functions. There have been several papers promoting the use of restricted mean survival time (RMST) in clinical trials. The arguments are (i) ease of interpretation (though I am not convinced a restricted mean is that easy to explain) and (ii) providing a simple summary in the presence of non-proportional hazards. See Royston and Parmar (2013) for a description of the use of the measure in RCTs.

The restricted mean survival time at time $t^*$ is defined as,
$$
E\left[min(t,t^*)\right]
$$
i.e. it is the mean up to some point $t^*$. The treatment effect in a RCT can be defined as the difference in RMST between the randomized arms at time $t^*$. The RMST can be estimated by calculating the area under the survival curve between 0 and $t^*$. In an observational study where we need to take account of potential confounders, we can define the RMST of the standardized survival function as

$$
RMST(t^*|X=x,Z) = \int_0^{t^*} E\left[S(t|X=x,Z)\right]
$$

and is estimated by

$$
\widehat{RMST}(t^*|X=x,Z) = \int_0^{t^*} \frac{1}{N}\sum\_{i=1}^{N}S(t|X=x,Z=z_i)]
$$

Contrasts between exposure groups can either be differences or ratios,

$$
\widehat{RMST}(t^*|X=1,Z) - \widehat{RMST}(t^*|X=0,Z) 
$$


$$
\frac{\widehat{RMST}(t^*|X=1,Z)}{\widehat{RMST}(t^*|X=0,Z)}
$$

Standardized RMST and contrasts is implemented in `standsurv` using the `rmst` option.

## Example

I will use the Rotterdam Breast cancer data. The code below loads and `stset`'s the data and then fits a model using `stpm3`. 

```stata
<<dd_do >>
use https://www.pclambert.net/data/rott3, clear
stset os, f(osi==1) scale(12) exit(time 120)
stpm3 i.hormon age enodes pr_1, scale(lncumhazard) df(4) eform nolog tvc(i.hormon) dftvc(3)
<</dd_do>>
```

I have made the effect of our exposure, `hormon`, time-dependent using the `tvc` option to illustrate that 
we can have interactions etc with our exposure in our model. This is an interaction with time, i.e. non proportional hazards.


I first calculate the standardized survival curves where everyone is forced to be exposed and then unexposed.

```stata
<<dd_do >>
range timevar 0 10 100
standsurv, surv timevar(timevar) ci frame(surv, replace)          ///
           at1(hormon 0) at2(hormon 1) atvar(S_hormon0 S_hormon1)

frame surv {
  twoway (area S_hormon0 timevar, sort fcolor(%30)) ///
         , legend(off)                              ///
         ylabel(0(0.1)1, format(%3.1f))             ///
         ytitle("S(t)")                             ///
         xtitle("Years from surgery")               ///
         title("No treatment")                      ///
         name(hormon0, replace)
  
  twoway (area S_hormon1 timevar, sort pstyle(p2line) fcolor(%30)) ///
         , legend(off)                                             ///
         ylabel(0(0.1)1, format(%3.1f))                            ///
         ytitle("S(t)")                                            ///
         xtitle("Years from surgery")                              ///
         title("Treatment")                                        ///
         name(hormon1, replace)
  		
  graph combine hormon0 hormon1, nocopies		
}
<</dd_do>>
```

<<dd_do: quietly>>
graph export ${DRIVE}/github/pclambert/statasvg/standsurv_rmst_stand.svg, replace width(700px)
<</dd_do>>

![](/statasvg/standsurv_rmst_stand.svg)

The RMST at 10 years for each of the standardized survival functions is the area under the standardized survival curve, shown by the shaded areas in the graphs above.

I will now run `standsurv` again with the `rmst` option to estimate these togther with the difference in RMST. I only want the RMST at 
10 years so create a variable `t_rmst10` with only one observation, equal to 10.

```stata
<<dd_do >>
gen t10 = 10 in 1
standsurv, rmst timevar(t10) ci frame(rmst, replace)          ///
           at1(hormon 0) at2(hormon 1) atvar(rmst_h0 rmst_h1) ///
           contrast(difference) contrastvar(rmstdiff)
<</dd_do>>
```

I will first list the standardized RMST in both treatment groups.

```stata
<<dd_do >>
frame rmst: list t10 rmst_h0* rmst_h1* in 1, noobs abb(12) 
<</dd_do>>
```

The RMST at 10 years is 7.54 years in those not taking treatment and 7.94 years in those taking treatment. The 95% confidence
intervals are also shown. As I used the `contrast(difference)` option I can look at the difference in RMST at 10 years.

```stata
<<dd_do >>
frame rmst: list t10	rmstdiff* in 1, noobs abb(12)
<</dd_do>>
```

The difference is 0.39 years (95% CI 0.12 to 0.67).


The RMST will vary by the choice of $t^*$. A range of values of $t^*$ can be given and then plotted. 


```stata
<<dd_do >>
range t_rmst 0 10 50
standsurv, rmst timevar(t_rmst) ci frame(rmst2, replace)      ///
           at1(hormon 0) at2(hormon 1) atvar(rmst_h0 rmst_h1) ///
           contrast(difference) contrastvar(rmstdiff)
<</dd_do>>
```

We can plot how the RMST changes and the difference in RMST changes as a function of $t^\*$.

```stata
<<dd_do >>
frame rmst2 {
  twoway (line rmst_h0 rmst_h1 t_rmst) ///
         , legend(order(1 "No treatment" 2 "Treatment") cols(1) pos(11)) ///
         ytitle("RMST (years)") ///
         xtitle("Years from surgery") ///
         name(RMST,replace)
  
  twoway (rarea rmstdiff_lci rmstdiff_uci t_rmst, color(blue%20)) ///
         (line rmstdiff t_rmst, lcolor(blue))                     ///
         , legend(off)                                            ///
         ylabel(, format(%3.1f))                                  ///
         ytitle("Difference in RMST (years)")                     ///
         xtitle("Years from surgery")                             ///
  		   name(RMSTdiff, replace)
  		
  graph combine RMST RMSTdiff, nocopies
}  
<</dd_do>>
```

<<dd_do: quietly>>
graph export ${DRIVE}/github/pclambert/statasvg/standsurv_RMST_diff.svg, replace width(700px)
<</dd_do>>

![](/statasvg/standsurv_RMST_diff.svg)

## References

Royston, P. Parmar, M. K. B. Restricted mean survival time: an alternative to the hazard ratio for the design and analysis of randomized trials with a time-to-event outcome. 
*BMC medical research methodology* 2013;**13**:152
